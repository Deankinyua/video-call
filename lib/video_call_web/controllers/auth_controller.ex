defmodule VideoCallWeb.AuthController do
  @moduledoc """
  Google authentication controller.

  This controller implements the following Ueberauth methods and logout method:
   - callback(%{assigns: %{ueberauth_failure: _fails}} = conn, _params)
   - callback(%{assigns: %{assigns: %{ueberauth_auth: auth} = conn, _params)

  These methods are responsible for fetching user data from GitHub provider
  along with user token.

  """

  use VideoCallWeb, :controller

  plug :store_return_to when action in [:request]
  plug Ueberauth

  import Plug.Conn

  alias VideoCall.Accounts
  alias VideoCallWeb.UserAuth

  @type conn :: Plug.Conn.t()
  @type params :: map()

  @doc """
  If the user authentication failes, it redirects to the return_to page or home page.
  When user successfully authenticates, it redirects the user to the return _to page or home page.
  """
  @spec callback(conn(), params()) :: conn()
  def callback(%{assigns: %{ueberauth_failure: _fails}} = conn, _params) do
    conn
    |> put_flash(:error, "User authentication failed!")
    |> redirect(to: ~p"/")
  end

  def callback(%{assigns: %{ueberauth_auth: auth}} = conn, _params) do
    google_token = auth.credentials.token

    with {:ok, user_params} <- user_info_from_auth(auth),
         {:ok, user} <- Accounts.get_or_create_user(user_params) do
      Accounts.clear_all_tokens_for_user(user)
      UserAuth.log_in_user(conn, user, %{"google_token" => google_token, "remember_me" => "true"})
    else
      _error ->
        redirect(conn, to: ~p"/")
    end
  end

  @doc """
  Logs the user out, removes user token from cookie session and clear token record from db.
  """
  @spec logout(conn(), params()) :: conn()
  def logout(conn, _params) do
    UserAuth.log_out_user(conn)
  end

  defp user_info_from_auth(%{info: info}) when is_map(info) do
    {:ok,
     %{
       avatar: info.image,
       email: info.email,
       username: generate_user_name(info.first_name)
     }}
  end

  defp user_info_from_auth(_auth), do: {:error, "Auth error"}

  defp generate_user_name(first_name) do
    suffix = "_" <> "#{:rand.uniform(10000)}"

    first_name <> suffix
  end

  defp store_return_to(%{params: %{"return_to" => return_to}} = conn, _opts)
       when is_binary(return_to),
       do: put_session(conn, :user_return_to, return_to)

  defp store_return_to(conn, _opts), do: conn
end
